plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "com.github.johnrengelman.shadow" version "8.1.1" apply false
    id "dev.architectury.loom" version "1.3-SNAPSHOT" apply false
    id "io.github.CDAGaming.cursegradle" version "1.6.1" apply false
    id "com.modrinth.minotaur" version "2.+" apply false
    id "io.github.juuxel.loom-vineflower" version "1.11.0" apply false
}

architectury {
    minecraft = project.mc_version
}

subprojects {
    apply plugin: "dev.architectury.loom"
    apply plugin: "com.github.johnrengelman.shadow"
    apply plugin: "com.matthewprenger.cursegradle"
    apply plugin: "com.modrinth.minotaur"
    apply plugin: "io.github.juuxel.loom-vineflower"

    loom {
        silentMojangMappingsLicense()
    }

    vineflower {
        toolVersion = "1.9.3"
    }

    dependencies {
        minecraft "com.mojang:minecraft:${mc_version}"
        mappings loom.officialMojangMappings()
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"

    archivesBaseName = project.archives_base_name
    version = project.mod_version
    group = project.maven_group

    tasks.withType(JavaCompile).tap {
        configureEach {
            options.encoding = "UTF-8"
            options.release = 17
        }
    }

    repositories {
        maven {
            name "CurseMaven"
            url "https://www.cursemaven.com/"
            content {
                includeGroup "curse.maven"
            }
        }
        maven {
            name "Modrinth Maven"
            url "https://api.modrinth.com/maven"
            content {
                includeGroup "maven.modrinth"
            }
        }
    }
}

tasks.register("curseforgeModrinthFabric") {
    group "publishing"
    dependsOn ":fabric:modrinth"
    dependsOn ":fabric:curseforge"
}

tasks.register("curseforgeModrinthForge") {
    group "publishing"
    dependsOn ":forge:modrinth"
    dependsOn ":forge:curseforge"
}